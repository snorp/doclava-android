/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import javax.tools.ToolProvider

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.android'
version = '1.0.6'

repositories {
    jcenter()
}

dependencies {
    compile 'org.antlr:antlr4:4.7.1'
    compile 'com.google.jsilver:jsilver:1.0.0'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'

    testCompile 'junit:junit:4.12'

    // tools.jar required for com.sun.javadoc
    compile files(((URLClassLoader) ToolProvider.getSystemToolClassLoader()).getURLs())
}

sourceSets {
    main {
        java.srcDirs = ['src/']
        resources.srcDirs = ['res/']
    }
    test {
        java.srcDirs = ['test/']
        resources.srcDirs = ['test/api']
    }
}

jar {
  manifest {
    attributes(
      'Main-Class': 'com.google.doclava.Doclava'
    )
  }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${buildDir}/repo"))
        }
    }
}

task dist(type: Zip, dependsOn: uploadArchives)  {
    group = BasePlugin.BUILD_GROUP
    description 'Builds distribution artifacts.'

    doLast {
        logger.lifecycle "Compressed maven artifacts to ${archivePath}"
    }
}
